// Preconditioned BiCGstab Applied to the system
// M^{-1}Aglobal x = M^{-1}b
// Here Aglobal denotes the global matrix
// M^{-1} is the RAS preconditioner based on domain decomposition
/*# debutBNNPrecond #*/
func real[int] RAS2(real[int] & r){
		real[int] z = Q(r);
		real[int] aux = RAS(r);
		z += aux;
	   return z;
}
func real[int] BNN(real[int] &u)   // precond BNN
{ 
    real[int] aux1 = Q(u);
    real[int] aux2 = P(u);
    real[int] aux3 = RAS(aux2);
    aux2 = PT(aux3);
    aux2 += aux1;	
	return aux2;	
}
/*# finBNNPrecond #*/
/*# debutBiCGSolve #*/ 
func real[int] BiCGstab(real[int] xi, real eps, int nbiter)
{
	ofstream filei("Convprec.m");
	Vh r, rb, rs, er, un, p, yn, zn, z, v, t, tn;
	un[]=xi;	
	r[]=A(un[]);
	r[] -= rhsglobal[];     
	r[] *= -1.0; 	
	rb[]=r[];	
	real rho=1.;
	real alpha=1.;
	real omega=1.;
	z[] = RAS2(r[]);
	real resinit=sqrt(z[]'*z[]);
	p[]=0; v[]=0;	
	real rhonew, beta;	
	for(int it =1;it<nbiter;it++)
	  {	
        real relres = sqrt(z[]'*z[])/resinit;
        cout << "It: "<< it << " Relative residual = " << relres  <<  endl; 
        int j = it+1;
        filei << "relres("+j+")=" << relres << ";" << endl;        
        if(relres < eps)
           {
            cout << "BiCGstab has converged in " + it + " iterations " << endl;
            cout << "The relative residual is " +  relres << endl; 
            break;	
           }	
        rhonew=rb[]'*r[];                               // rhoi = (rb,rim1);             
        beta=(rhonew/rho)*(alpha/omega); // beta = (rhoi/rhoim1)*(alpha/omega); 
        p[] -=omega*v[];                               // pi = rim1 + beta*(pim1 - omega*vim1);
        p[] *= beta;
        p[] += r[];
        yn[] = RAS2(p[]);                               // y = Mm1*pi; vi = A*y;
        v[] = A(yn[]);
        alpha = rhonew/(rb[]'*v[]);              // alpha = rhoi/(rb'*vi);	 
        rs[] = r[];                                            // s = rim1 - alpha*vi;
        rs[] -= alpha*v[];
        zn[] = RAS2(rs[]);                              // z = Mm1*s; t = A*z;
        t[] = A(zn[]);
        tn[] = RAS2(t[]);                                  // tn = Mm1*t;
        omega = (tn[]'*zn[])/(tn[]'*tn[]);         // omega = (Mm1*t,Mm1*s)/(Mm1*t,Mm1*t);
        un[] += alpha*yn[];                           // xi = xim1 + alpha*y + omega*z;
        un[] += omega*zn[]; 
        r[] = rs[];
        r[] -= omega*t[];
        z[] = RAS2(r[]);     
        rho = rhonew;
     }
     return un[];	
}
/*# finBiCGSolve #*/